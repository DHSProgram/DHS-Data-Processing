PROC GLOBAL
{+--------------------------------------------------------------------------+}
{+                                                                          +}
{+   Guidelines July 7, 2022         -       Version 1.0.0 of 12/08/2022    +}
{+                                                                          +}
{+   CHAPTER NB.  NEWBORN MODULE                                            +}
{+                                                                          +}
{+   Table NB.1   Bathing newborns                                          +}
{+   Table NB.2   Cord cutting                                              +}
{+   Table NB.3   Cord care                                                 +}
{+   Table CH.1   Use of chlorhexidine                                      +}
{+                                                                          +}
{+--------------------------------------------------------------------------+}

  numeric i, itot, imax, j, jmin, jmax, jtot, jtot1, jtot2;
  numeric xtemp, x, age, ideliv, rweight; 

  crosstab float(0) txxx unweight runday+runmonth+runyear
    exclude(specval, rowzero, colzero, totals, percents)
{+EN}
    title( "Tables for chapter Newborn, Country Survey 2020" );
{EN+}
{{ES}
{ES}}
{{FR}
{FR}}  

  crosstab float(1) tnb01 agenb+delivnb+total timebath+total+numbirth
    exclude(rowzero,colzero,percents,totals,specval)
{+EN}
    title( "Table NB.1 Bathing newborns","",
		   "Among most recent live births in the 2 years preceding the survey,", 
		   "percent distribution by timing of first bath, according to", 
		   "background characteristics, Country Survey 2020" )
     stub( "Background characteristic" );
{EN+}
{{ES}
{ES}}
{{FR}
{FR}}
  crosstab float(0) tnb01u agenb+delivnb+total total
    exclude(rowzero,colzero,percents,totals,specval)
    title( "Table 5.1 Media exposure to malaria messages (Number of un-weighted cases)" )
     stub( "Background characteristic" );

  crosstab float(1) tnb02 agenb+border2+v102w+v101w+v106wt+v190w+total 
                          cutcord+total+cutclean+numbrth3
    exclude(rowzero,colzero,percents,totals,specval)
{+EN}
    title( "Table NB.2  Cord cutting","",
		   "Among most recent births in the 2 years preceding the survey that were delivered", 
		   "outside a health facility, percent distribution by instrument used to cut the", 
		   "umbilical cord, and percentage with umbilical cord cut with a clean instrument,", 
		   "according to background characteristics, Country Survey 2020" )
     stub( "Background characteristic" );
{EN+}
{{ES}
{ES}}
{{FR}
{FR}}
  crosstab float(0) tnb02u agenb+border2+v102w+v101w+v106wt+v190w+total total
    exclude(rowzero,colzero,percents,totals,specval)
    title( "Table NB.2  Cord cutting (Number of un-weighted cases)" )
     stub( "Background characteristic" );

  crosstab float(1) tnb03 agenb+delivnb+border2+v102w+v101w+v106wt+v190w+total 
                          substan+noharm+numbirth
    exclude(rowzero,colzero,percents,totals,specval)
{+EN}
    title( "Table NB.3 Cord care","",
		   "Among most recent live births in the 2 years preceding the survey, percentage", 
		   "with different substances applied to the stump of the umbilical cord, and", 
		   "percentage with nothing harmful applied to the umbilical cord,", 
		   "according to background characteristics, Country Survey 2020" )
     stub( "Background characteristic" );
{EN+}
{{ES}
{ES}}
{{FR}
{FR}}
  crosstab float(0) tnb03u agenb+delivnb+border2+v102w+v101w+v106wt+v190w+total total
    exclude(rowzero,colzero,percents,totals,specval)
    title( "Table NB.3 Cord care (Number of un-weighted cases)" )
     stub( "Background characteristic" );

  crosstab float(1) tch01 agenb+delivnb+border2+v102w+v101w+v106wt+v190w+total 
                          chlorhex+chlodays+total+numbrth4
    exclude(rowzero,colzero,percents,totals,specval)
{+EN}
    title( "Table CH.1 Use of chlorhexidine","",
		   "Among most recent live births in the 2 years preceding the survey, percentage", 
		   "with chlorhexidine applied to the stump of the umbilical cord, and percentage", 
		   "with chlorhexidine applied to the stump of the umbilical cord within 24 hours", 
		   "after birth; and among most recent live births in the 2 years preceding the", 
		   "survey with chlorhexidine applied to the stump of the umbilical cord, percent", 
		   "distribution by number of days chlorhexidine was applied,", 
		   "according to background characteristics, Country Survey 2020" )
     stub( "Background characteristic" );
{EN+}
{{ES}
{ES}}
{{FR}
{FR}}
  crosstab float(0) tch01u agenb+delivnb+border2+v102w+v101w+v106wt+v190w+total coltotu
    exclude(rowzero,colzero,percents,totals,specval)
    title( "Table CH.1 Use of chlorhexidine (Number of un-weighted cases)" )
     stub( "Background characteristic" );

  { function to recode birth order as it is used by several tables }
  function RecBorder( chi )
    xtemp = BORD(chi);
    { adjust multiple births to give the same order as that of the first of the multiples }
    if B0(chi) > 1 then xtemp = BORD(chi) - B0(chi) + 1 endif;
    recode xtemp -> x;
              1  -> 0;
            2:3  -> 1;
            4:5  -> 2;
                 -> 3;
    endrecode;
    RecBorder = x;
  end;

  { function to recode mother's age as it is used by several tables }
  function RecAge( chi )
    age = int( (P3(chi)-V011)/12 );
    recode  age  -> x;
          <20 -> 1;
        20:34 -> 2;
              -> 3;
    endrecode;
    RecAge = x;
  end;
  
  { function to recode place of delivery }
  function RecDeliver( chi )
    xtemp = M15(chi);
    recode xtemp -> x;
           20:29 -> 1;        { public sector }
           30:39 -> 2;        { private sector }
           40:49 -> 3;        { NGO sector }
                 -> 4;
    endrecode;
	RecDeliver = x;
  end;	

  { define use of other substances }
  function OtherSubstances()
    OtherSubstances = ( 1 in MNB7C,MNB7D,MNB7E,MNB7F,MNB7G,MNB7H,MNB7I,MNB7J,MNB7X,MNB7Z );
  end; 


PROC RECODE8_FF
preproc

  SetLanguage("EN");    // !!!change to language to be used from valuesets in working dicionary

  total    = 0;
  numwom   = 1;

  unweight = ( sysparm()[1:1] = "U" );   { 0-Weighted, 1-unweighted }

postproc

  { constructs table to determine whether run is weighted/unweighted }
  txxx(unweight,0) = sysdate( "dd" );      { day   }
  txxx(unweight,1) = sysdate( "mm" );      { month }
  txxx(unweight,2) = sysdate( "yyyy" );    { year  }

  { Table NB.1 processing }
  ideliv = tblrow( tnb01, delivnb = 0 );
  tnb01[ideliv,*] = tblsum( row tnb01[ideliv+1:ideliv+3,*] );
  jtot  = tblcol( tnb01 );
  jmax  = jtot - 2;
  do j = 0 while j <= jmax
    tnb01[*,j] = tnb01[*,j] * 100 / tnb01[*,jtot];
  enddo;
  tnb01[*,jmax+1] = tblsum( column tnb01[*,0:jmax] );
  { check unweighted N's }
  tnb01u[ideliv,*] = tblsum( row tnb01u[ideliv+1:ideliv+3,*] );
  Col2Dim( "tnb01", tnb01, 0, jtot-3, tnb01u, 0 );

  { Table NB.2 processing }
  jtot  = tblcol( tnb02 );
  jmax  = jtot - 1;
  do j = 0 while j <= jmax
    tnb02[*,j] = tnb02[*,j] * 100 / tnb02[*,jtot];
  enddo;
  tnb02[*,jmax-1] = tblsum( column tnb02[*,0:jmax-2] );
  { check unweighted N's }
  Col2Dim( "tnb02", tnb02, 0,      jtot-3, tnb02u, 0 );
  Col2Dim( "tnb02", tnb02, jtot-1, jtot-1, tnb02u, 0 );

  { Table NB.3 processing }
  ideliv = tblrow( tnb03, delivnb = 0 );
  tnb03[ideliv,*] = tblsum( row tnb03[ideliv+1:ideliv+3,*] );
  jtot  = tblcol( tnb03 );
  jmax  = jtot - 1;
  do j = 0 while j <= jmax
    tnb03[*,j] = tnb03[*,j] * 100 / tnb03[*,jtot];
  enddo;
  { check unweighted N's }
  tnb03u[ideliv,*] = tblsum( row tnb03u[ideliv+1:ideliv+3,*] );
  Col2Dim( "tnb03", tnb03, 0, jtot-1, tnb03u, 0 );

  { Table CH.1 processing }
  ideliv = tblrow( tch01, delivnb = 0 );
  tch01[ideliv,*] = tblsum( row tch01[ideliv+1:ideliv+3,*] );
  jtot1 = tblcol( tch01, chlorhex );
  jmax  = jtot1 - 1;
  do j = 0 while j <= jmax
    tch01[*,j] = tch01[*,j] * 100 / tch01[*,jtot1];
  enddo;
  jtot2 = tblcol( tch01 );
  jmax  = jtot2 - 2;
  do j = jtot1+1 while j <= jmax
    tch01[*,j] = tch01[*,j] * 100 / tch01[*,jtot2];
  enddo;
  tch01[*,jmax+1] = tblsum( column tch01[*,jtot1+1:jmax] );
  { check unweighted N's }
  tch01u[ideliv,*] = tblsum( row tch01u[ideliv+1:ideliv+3,*] );
  Col2Dim( "tch01", tch01, 0,       jtot1-1, tch01u, 0 );
  Col2Dim( "tch01", tch01, jtot1+1, jtot2-2, tch01u, 1 );
  
PROC HOUSEHOLD
preproc

  if HV015 <> 1 then skip case; endif;
 
PROC WOMAN
preproc

  if V015 <> 1 then skip case; endif;

postproc

  if unweight then
    rweight = 1;
  else
    rweight = V005 / 1000000;
 endif;

  { general variables }
  v101w  = V101;
  v102w  = V102;
  v013w1 = V013;               { with var label for printing purposes }
  v106w  = V106;
  if V106 in 8,missing then v106w = 9 endif;
  v106wt = v106w;
  v190w = V190;

{ -------------------------------------------------------------------- }
{ tables NB.1 }

  { !!!! missing NGO in facility type and labels }
  numbirth = 1;
  for i in REC41_EDT do
    if M80 in 1 & P19(MIDXP) < 24 then  // last birth in the last 2 years
      agenb   = RecAge( MIDXP );        { mother's age at birth }
      recode  MNB1  -> timebath;
               0    -> 1;
            100:105	-> 1;
            106:123	-> 2;
            124:230	-> 3;
              998   -> 8;			  
                    -> default;
      endrecode;				
      delivnb = RecDeliver(i);          // place of delivery
      xtab( tnb01, rweight );
      xtab( tnb01u );	
    endif;
  enddo;	

{ -------------------------------------------------------------------- }
{ tables NB.2 }

  { !!! 100% not possible because missing DK combinations with MNB4 and MNB5 }
  for i in REC41_EDT do
    cutcord  = notappl;
    cutclean = notappl;
    numbrth3 = 1;
    if M80 in 1 & P19(MIDXP) < 24 & M15 in 11,12,96 then  // last birth in last 2 years delivered outside health facility
      border2 = RecBorder( MIDX );       { birth order }
      agenb   = RecAge( MIDXP );         { mother's age at birth }
      xtab( tnb02, rweight );
      xtab( tnb02u );	
      numbrth3 = notappl;
      if MNB3 in 1:3 & MNB4 = 1 then
        cutcord = 1;	  
      elseif MNB3 in 1:3 & MNB4 in 2,8 & MNB5 = 1 then
        cutcord = 2;	  
      elseif MNB3 in 1:3 & MNB4 = 2 & MNB5 = 0 then
        cutcord = 3;	  
      else
        cutcord = 4;	  
      endif;	  
      xtab( tnb02, rweight );
	  if cutcord in 1,2 then
	    cutcord  = notappl;
	    cutclean = 1;
        xtab( tnb02, rweight );
      endif;		
    endif;
  enddo;	

{ -------------------------------------------------------------------- }
{ tables NB.3 }

  for i in REC41_EDT do
    substan  = notappl;
    noharm   = notappl;
    numbirth = 1;
    if M80 in 1 & P19(MIDXP) < 24 then    // last birth in last 2 years 
      border2 = RecBorder( MIDX );        // birth order 
      agenb   = RecAge( MIDXP );          // mother's age at birth 
      delivnb = RecDeliver(i);            // place of delivery
      xtab( tnb03, rweight );
      xtab( tnb03u );	
      numbirth = notappl;
      if MNB6 = 0 then
        substan = 1;	  
        xtab( tnb03, rweight );
      endif; 		
      if MNB7A = 1 | MNB10 = 1 then
        substan = 2;	  
        xtab( tnb03, rweight );
      endif; 		
      if MNB7B = 1 then
        substan = 3;	  
        xtab( tnb03, rweight );
      endif; 		
      if OtherSubstances() then
        substan = 4;	  
        xtab( tnb03, rweight );
      endif;	  
      substan  = notappl;
      if MNB6 = 0 | MNB6 = 1 & !OtherSubstances() then
        noharm = 1;	  
        xtab( tnb03, rweight );
      endif;		
    endif;
  enddo;	

{ -------------------------------------------------------------------- }
{ tables CH.1 }

  for i in REC41_EDT do
    chlodays = notappl;
    numbrth4 = notappl;
    if M80 in 1 & P19(MIDXP) < 24 then  // last birth in last 2 years 
      border2 = RecBorder( MIDX );      { birth order }
      agenb   = RecAge( MIDXP );        { mother's age at birth }
      delivnb = RecDeliver(i);          // place of delivery
	  chlorhex = 3;
      xtab( tch01, rweight );
	  coltotu = 1;
      xtab( tch01u );
      if MNB7A = 1 | MNB10 = 1 then     // chlorexidine applied	  
        chlorhex = 1;
        xtab( tch01, rweight );
      endif;		
      if MNB11 < 124 then               // chlorexidine applied	witin 24 hours  
        chlorhex = 2;
        xtab( tch01, rweight );
      endif;		
      chlorhex = notappl;
      if MNB7A = 1 | MNB10 = 1 then     // chlorexidine applied	
        numbrth4 = 1;	  
        recode MNB12 -> chlodays;
                 1   -> 1;
                2:6  -> 2;              // !!! tabplan includes 7
                 7   -> 3;	
                 8   -> 8;
			         -> default;
        endrecode;				   
        xtab( tch01, rweight );
        coltotu = 2;
        xtab( tch01u );
      endif;	  
    endif;
  enddo;	
