{Application 'WRKELIG' logic file generated by CSPro }
PROC GLOBAL

  numeric found, xhhnum, xtype, xintsex, i;

  alpha(10)  key_hogar;                     { !! adjust to the length of the key cluster+hh+line }
  string     fnameHH, fnameIN;              { to extract file name from household and individual dictionaries }

PROC FL_CNULL
preproc

  { converts parameter to numbers  }
  xhhnum   = tonumber( sysparm()[1:4] );   { household number }
  xtype    = tonumber( sysparm()[5:1] );   { type of selection 1-household, 2-women }
  xintsex  = tonumber( sysparm()[6:1] );   { interviewer's sex to determine what individuals to select }
  SetLanguage( GetLanguage() );

  fnameHH = filename( ccHH80 );     { get the file name from the household dictionary }
  fnameIN = filename( ccIN80 );     { get the file name from the individual dictionary }

  open( ELIGINDV, create );

  open( CCHH80 );
  while loadcase( CCHH80 ) do
    { households requested }
    if xtype = 1 then
      key_hogar = edit("9999",AHCLUST) + edit("9999",AHNUMBER) + "  ";
      if loadcase( ELIGINDV, key_hogar) then
        delcase( ELIGINDV );
      endif;
      ZCLUSTER = AHCLUST;
      ZNUMBER  = AHNUMBER;
      ZLINE    = notappl;
      ZNAME    = " ";
      ZQTYPEA  = " ";
      ZRESULT  = AHRESULT;
      ZOWNER   = AHINTNUM;
      if soccurs( CCHH80.AHSEC01X ) then
        ZNAME = strip(AHFIRSTN(1)) + " " + strip(AHLASTN(1));
      endif;
      ZADDRESS = AHADDRESS;
	  if length( strip(ZNAME) ) = 0 then ZNAME = ZADDRESS endif;
      if AHRESULT <> 1 then
        ZSTATUS  = tr("Incomplete");
        ZSTATUSC = 2;
      elseif partialcs( 1, AHCLUST, AHNUMBER, 0, fnameHH  ) then
        ZSTATUS  = tr("Partial");
        ZSTATUSC = 3;
      else
        ZSTATUS  = tr("Complete");
        ZSTATUSC = 1;
      endif;
      ZQUEST  = 1;
      writecase( ELIGINDV );
	    
    { individuals requested }
    elseif xhhnum = AHNUMBER then
      ZADDRESS = AHADDRESS;
      if !length(strip(AHADDRESS)) & soccurs( CCHH80.AHSEC01X ) then
        ZADDRESS = strip(AHFIRSTN(1)) + " " + strip(AHLASTN(1));
      endif;
      for i in record AHSEC01 do
        { eligible individuals according to interviewer's sex.  3 is used by supervisor menu as they can see both individuals }
//        if AH09 & xintsex = 2 | AH10 & xintsex = 1 | ( AH09 | AH10 ) & xintsex = 3 then
        if AH09 | AH10 then    
          if AH09 then
            key_hogar = edit("9999",AHCLUST) + edit("9999",AHNUMBER) + edit("99",AH09);
          else
            key_hogar = edit("9999",AHCLUST) + edit("9999",AHNUMBER) + edit("99",AH10);
          endif;
          if loadcase( ELIGINDV, key_hogar ) then
            delcase( ELIGINDV );
          endif;
          ZCLUSTER = AHCLUST;
           ZNUMBER  = AHNUMBER;
          if AH09 then
            ZLINE  = AH09;
            ZQTYPEA  = tr("Female");
          else
            ZLINE  = AH10;
            ZQTYPEA  = tr("Male");
          endif;
          ZNAME    = AH02;
          ZQUEST   = 2;
          ZOWNER   = notappl;
          ZSTATUS  = tr("Not Visited");
          ZSTATUSC = 0;
          if loadcase( ccIN80, key_hogar) then
            ZOWNER   = AINTNUM;
            ZRESULT  = ARESULT;
            if ARESULT <> 1 then
              ZSTATUS  = tr("Incomplete");
              ZSTATUSC = 2;
            elseif partialcs( 2, ACLUSTER, ANUMBER, ALINE, fnameIN  ) then
              ZSTATUS  = tr("Partial");
              ZSTATUSC = 3;
            else
              ZSTATUS  = tr("Complete");
              ZSTATUSC = 1;
            endif;
            ZOWNER  = AINTNUM;
          endif;

          writecase( ELIGINDV );
        endif;
      enddo;
      break;
    endif;
  enddo;
  close( ELIGINDV );
  close( CCHH80 );
  stop(1);
