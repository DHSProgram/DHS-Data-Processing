 {Application 'ALLEXPORTS' logic file generated by CSPro }
PROC GLOBAL

  relation persons  AHSEC01 to ABSEC01  where AH01 = AB102     { Biomarker Children }
                            to ABSEC02  where AH01 = AB202     { Biomarker women }
                            to ABSEC03  where AH01 = AB302     { Biomarker men }
                            to AHSECA1  where AH01 = AHAi1L    { Accident and injury _ road traffic accidents }							
                            to AHSECA2  where AH01 = AHAi17L   { Accident and injury _ other accidents }							
                            to AHSECCL  where AH01 = ACHLINE   { Child labor }
                            to AHSECCD  where AH01 = ACDi2     { Child discipline }							
                            to AHSECHE1 where AH01 = AH203     { Inpat  -Health expenditure }							
                            to AHSECHE2 where AH01 = AH302;    { outpat -Health expenditure }							

  relation children AWSEC2B to AWSEC04  where A215L = A404     { Pregnancy and postnatal care }
                            to AWSEC05  where A215l = A503     { Vaccines }
                            to AWSEC6A  where A215l = A603     { Child health }
                            to AWSECG2  where A215l = LineGC   { FGC }
                            to AWSECECD where A215l = AECDCA;  { Early childhood development }

  relation kids36   AWSEC04 to AWSEC2B  where A404 = A215L    { Pregnancy history }
                            to AWSEC05  where A404 = A503     { Vaccines }
                            to AWSEC6A  where A404 = A603     { Child health }
							to AWSEC6B  where A404 = A635     { Feeding practice }
                            to AWSECG2  where A404 = LineGC   { FGC }
                            to AWSECECD where A404 = AECDCA;  { Early childhood development }
							
  relation kids60   AWSEC6A to AWSEC2B  where A603 = A215L    { Pregnancy history }
                            to AWSECG2  where A603 = LineGC   { FGC }
                            to AWSECECD where A603 = AECDCA;  { Early childhood development }

  FILE f1, f2, f3, f4, f5, f6, f7;

  numeric i;
  numeric DaysMonth = 30.4375;

PROC ccIQ81_FF
preproc

  setfile( f1, ".\households.dat" );
  setfile( f2, ".\persons.dat" );
  setfile( f3, ".\women.dat" );
  setfile( f4, ".\children.dat" );
  setfile( f5, ".\kids_35.dat" );
  setfile( f7, ".\kids_59.dat" );
  setfile( f6, ".\men.dat" );

  set behavior() export (all {SPSS} SubItemOnly );

PROC HOUSEHOLD
preproc

  { EXPORT HOUSEHOLDS }
  export to f1 case_id( AHCLUST, AHNUMBER ), 
               AHSECOVER,       { Cover page }
			   AHSEC02,         { Household characteristics }
			   AHSEC04,         { Additional household characteristics }
			   AHSEC03,         { Mosquito nets }
			   AHSECFS;         { Food security }
			   
  { EXPORT PERSONS IN HOUSEHOLDS }
  for i in relation PERSONS do
    export to f2 case_id( AHCLUST, AHNUMBER, AH01 ),
                 AHSECOVER,                                  { Cover page }  
				 AHSEC02,                                    { household characteristics }   
				 AHSEC04,                                    { additional household characteristics }   
				 AHSECFS,                                    { food security }
                 AHSEC01  exclude(AH01),                     { Household schedule } 
				 ABSEC01  exclude(ABCOL1, AB102),            { Biomarker Children }
                 ABSEC02  exclude(ABCOL2, AB202),            { Biomarker women }
				 ABSEC03  exclude(ABCOL3, AB302),            { Biomarker men }
                 AHSECA1  exclude(AHAi1L),                   { Accident and injury _ road traffic accidents }			
                 AHSECA2  exclude(AHAi17L),                  { Accident and injury _ other accidents }	
                 AHSECCL  exclude(ACHLine),                  { Child labor }
                 AHSECCD  exclude(ACDi2),                    { Child discipline }					
                 AHSECHE1 exclude(AH203L, AH203),            { Inpat  -Health expenditure }			
                 AHSECHE2 exclude(AH302);                    { outpat -Health expenditure }			
                 
  enddo;

PROC INDIVIDUAL
preproc

  { skip non-defacto individuals }
  if AH06(ALINE) <> 1 then skip case; endif;

  { exports coming from second level node }
  if AQTYPE  = 2 then
  
    { EXPORT WOMEN }
    export to f3 case_id( AHCLUST, AHNUMBER, ALINE ),
                 ASECOVER,  { Cover page }
				 AWSEC01,   { background characteristics } 
				 AWSEC2A,   { Reproduction - sec1 }
				 AWSEC2D,   { Reproduction - sec3 - currently pregnant }
				 AWSEC3A,   { Contraceptive table }
				 AWSEC3B,   { Contraceptive practice }
                 AWSEC6B,   { Feeding practice for child and woman }
				 AWSEC07,   { Marriage and sexual activities }
				 AWSEC08,   { Fertility preference }
				 AWSEC09,   { Husband and women background }
				 AWSEC10,   { HIV/AID knowledge }
				 AWSEC11,   { Other health issues }
				 AWSECCHD,  { Chronic diseases }
				 AWSECMH,   { Mental health }
       {!!!!}    AWSECM2,   { Maternal mortality }  
       {!!!!}    AWSECM3,   { Maternal mortality }  
				 AWSECDV,   { Domestic violence }
				 AWSECG1,   { FGC }
				 AWSECECD,  { Early childhood development }
				 AWSECSBC,  { Social behavior change - Malaria }
				 AWSECFIS;  { Fistula }

    { EXPORT CHILDREN - PREGNANCY HISTORY }
    for i in relation children do
      export to f4 case_id( AHCLUST, AHNUMBER, ALINE, A215L ),
                   AWSEC2B  exclude(A215L),                        { Pregnancy history }
                   AWSEC04  exclude(ACOL41,A404),                  { Pregnancy and postnatal care }
                   AWSEC05  exclude(ACOL51,A503),                  { Vaccination }
                   AWSEC6A  exclude(ACOL61,A603),                  { Child health }
                   AWSECG2  exclude(LINEGC,LineGC),                { FGC }
                   AWSECECD exclude(ACOLECD,AECDCA);               { Early childhood development } 
    enddo;

    { EXPORT CHILDREN UNDER 36 MONTHS }
    for i in relation kids36 do
      if ( AINTCD - A220CD ) / DaysMonth < 60 then
        export to f5 case_id( AHCLUST, AHNUMBER, ALINE, A212 ),
                     ASECOVER include(AINTC),                         { Cover page } 
                     AWSEC2B  exclude(A215L),                         { Pregnancy history }
                     AWSEC04  exclude(ACOL41, A404),                  { Pregnancy and postnatal care }
                     AWSEC05  exclude(ACOL51, A503),                  { Vaccination }
                     AWSEC6A  exclude(ACOL61, A603),                  { Child health }
					 AWSEC6B  exclude(A635,
					                  A643A,A643B,A643C,A643D,A643E,  { Exclude women feeding variables }
									  A643F,A643G,A643H,A643I,A643J,
									  A643K,A643L,A643M,A643N,A643O,
									  A643P,A643Q,A643R,A643S,A643T,
									  A643U,A643V,A643W,A643X,A643XA,
									  A643Y),
                     AWSECG2  exclude(LINEGC, LineGC),                { FGC }
                     AWSECECD exclude(ACOLECD,AECDCA);                { Early childhood development } 
      endif;
    enddo;
	
    { EXPORT CHILDREN UNDER 60 MONTHS }
    for i in relation kids60 do
      if ( AINTCD - A220CD ) / DaysMonth < 60 then
        export to f6 case_id( AHCLUST, AHNUMBER, ALINE, A212 ),
                     ASECOVER include(AINTC),                         { Cover page } 
                     AWSEC2B  exclude(A215L),                         { Pregnancy history }
                     AWSEC6A  exclude(ACOL61, A603),                  { Child health }
                     AWSECG2  exclude(LINEGC, LineGC),                { FGC }
                     AWSECECD exclude(ACOLECD,AECDCA);                { Early childhood development } 
      endif;
    enddo;	
	
	
  else
    { EXPORT MEN }
    export to f7 case_id( AHCLUST, AHNUMBER, ALINE ),
                 ASECOVER,              { Cover page } 
				 AMSEC01,               { Background characteristics } 
				 AMSEC02,               { Reproductive }  
				 AMSEC3A,               { Contraceptive table } 
				 AMSEC3B,               { Contraceptive practice } 
				 AMSEC04,               { Marriange and secual activities } 
                 AMSEC05,               { Fertility preference } 
				 AMSEC06,               { Employment and gender role } 
				 AMSEC07,               { HIV/AID knowledge } 
				 AMSEC08,               { Other health issues } 
				 AMSECCHD,              { Chronic diseases } 
				 AMSECMH,               { Mental Health } 				 
				 AMSECGC;               { GC }
  endif;

