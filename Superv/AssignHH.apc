{ Application 'ASSIGNHH' logic file generated by CSPro }
PROC GLOBAL

  { !!! if field structure (XSTRUCT/VSTRUCT) not available, remove it from form AssignHH.fmf }
  { !!! it is also necessary to remove them from this application and in SelCase in DCMenu.app }
  
  string  textstr, oldvar;

  numeric i, n, xteam, oldval, advancend;
  valueset fieldworkers;

  { set up basic user bar }
  function userbase();
    userbar( clear );
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );
    userbar( add button, ">>|",  do("AdvanceToEnd") );
    userbar( add button, "Lang", do("ChangeLanguage") );
  end;

  { set value sets based on language }
  function OnChangeLanguage()
    SetLanguage( getlanguage() );
  end;

  { function to populate value sets with proper interviewers }
  function populate()
    fieldworkers.clear();
    fieldworkers.add( tr("Not to be assigned yet"), 0 );
    { loops over interviewers file }
    while loadcase( INTERV ) do
      if ITEAM = xteam then
        fieldworkers.add( INAME, ICODE );
      endif;
    enddo;
    fieldworkers.add( tr("Advance to end"), 9998 );
  end;

PROC SAMPSEL_FF
preproc

  xteam = tonumber( sysparm()[1:2] );    { fieldwork team }
  
  advancend = 0;                       { to control option when advancing to end }

  { set up minimal user bar }
  userbase();
  userbar( show );
  SetLanguage( getlanguage() );

  setproperty(SAMPSEL, "ShowExtendedControlTitle", "No");

  { set font for value sets }
  setfont( All, "Arial", 18, bold );

  populate();

postproc
  stop(1);

PROC XCHANGE
preproc
  $ = 1;

postproc
  if $ = 1 then                                                               { review table of assignments }
    advance XINTCODE(XTOTAL);
  elseif $ = 2 then                                                           { assign interviewers }
    set attributes( XINTCODE ) autoskip;
    set attributes( XNUMBER, XSTRUCT, XADDRESS, XNAME, XMALE ) native;
  elseif $ = 3 then                                                           { change household address }
    set attributes( XADDRESS ) autoskip;
    set attributes( XNUMBER, XSTRUCT, XNAME, XMALE, XINTCODE ) native;
  elseif $ = 4 then                                                           { change names of heads of households }
    set attributes( XNAME ) autoskip;
    set attributes( XNUMBER, XSTRUCT, XADDRESS, XMALE, XINTCODE ) native;
  elseif $ = 5 then                                                           { add a household }
    XTOTAL = XTOTAL + 1;
    set attributes( XNUMBER, XSTRUCT, XADDRESS, XNAME, XMALE, XINTCODE ) autoskip;
    advance to XNUMBER(XTOTAL);
  elseif $ = 6 then                                                           { change selection for male survey }
    errmsg( 005 );
    reenter;
    set attributes( XMALE ) autoskip;
    set attributes( XNUMBER, XSTRUCT, XADDRESS, XNAME, XINTCODE ) native;
  elseif $ = 9 then                                                           { return to main menu without making modifications }
    stop(1)
  endif;

PROC SAMREC2000
  { first delete all records to get rid of potential garbage }
  while loadcase( HHFORINT ) do
    delcase( HHFORINT );
  enddo;
  { regenerates file for households assigned to interviewers }
  do i = 1 while i <= XTOTAL
    VNUMBER  = XNUMBER(i);
    VSTRUCT  = XSTRUCT(i);
    VADDRESS = XADDRESS(i);
    VNAME    = XNAME(i);
    VMALE    = XMALE(i);
    VINTCODE = XINTCODE(i);
    VRESULT  = XRESULT(i);
    writecase( HHFORINT );
  enddo;
  { updates total households to the supervisor control file }
  SSAMPLE = XCLUSTER;
  if loadcase( CONTSUP, SSAMPLE ) then
    if SHTOTAL = 0 | SHTOTAL <> XTOTAL then
      SHTOTAL = XTOTAL;
    endif;	  
	if SCRDATE = 0 & count(SAMREC2000 where XINTCODE <> 0) then
	  SCRDATE = sysdate("YYYYMMDD");
	endif;
    writecase( CONTSUP );
  endif;
  endlevel;

PROC XNUMBER
preproc
  if curocc() > XTOTAL then
    endsect;
  endif;

postproc
  { chack for duplicates when adding households }
  if XCHANGE = 5 then
    do i = 1 while i < curocc()
      if $ = $(i) then
	    errmsg( 015 );
        $ = notappl;
        XTOTAL = XTOTAL - 1;
        reenter XCHANGE
      endif;
    enddo;
  endif;

PROC XADDRESS
preproc
  oldvar = XADDRESS;
  if XCHANGE = 3 then
    set attributes( $ ) autoskip;
  endif;

postproc
  if XCHANGE = 3 & !length( strip($) ) then
    $ = oldvar
  endif;

PROC XNAME
preproc
  oldvar = XNAME;
  if XCHANGE = 4 then
    set attributes( $ ) autoskip;
  endif;

postproc
  if XCHANGE = 4 & !length( strip($) ) then
    $ = oldvar
  endif;

PROC XMALE
preproc
  n = curocc();
  oldval = visualvalue($(n));

onfocus
  textstr = strip( XNAME );
  if length( strip(XADDRESS) ) then
    textstr = textstr + " " + tr("and lives at address") + " " + strip(XADDRESS);
  endif;

postproc
  if XCHANGE = 6 & oldval = 1 & $ = 2 then
    errmsg( 010 );
    $ = 1;
    reenter XCHANGE;
  endif;

PROC XRESULT
preproc
  if curocc() = XTOTAL & XCHANGE = 5 then
    $ = 0;
  endif;

PROC XINTCODE
preproc
  if curocc() = XTOTAL & XCHANGE = 5 then
    $ = 0;
  elseif curocc() = XTOTAL & advancend then
    noinput;
  endif;

onfocus
  n = curocc();
  oldval = VisualValue( $(n) );
  { dynamically set value set }
  SetValueSet( $, fieldworkers );
  { prepares string to be displayed in question area }
  textstr = strip( XNAME );
  if length( strip(XADDRESS) ) then
    textstr = textstr + " " + tr("and lives at address") + " " + strip(XADDRESS);
  endif;

postproc
  if curocc() = XTOTAL & $ = 9998 then
    $ = oldval;
  elseif  $ = 9998 then
    $ = oldval;
    advancend = 1;
    advance to $(XTOTAL)
  elseif oldval <> $ & XRESULT = 1 then  { HH assigment can't be changed for questionnaires with result complete } 
    $ = oldval;
    errmsg(26, oldval);          
    reenter;
  elseif $ then
    ICODE = $;
    if loadcase( INTERV, ICODE ) then
      if XMALE <> 1 & ISEX = 1 then
	    errmsg( 020 );
      endif;
    endif;
    { set date the household was assigned to an interviewer }
    if oldval <> $ then
      XINTDATE(n) = sysdate( "YYYYMMDD" );
      if oldval <> 0 then
        errmsg( 025, oldval, $);
      endif;
    endif;
  endif;
  if curocc() = XTOTAL then
    if XCHANGE = 5 then            { add result "Not visited" to new households }
      XRESULT  = 0;
      XINTDATE = 0;
    endif;
    if XCHANGE = 1 then
      show( SAMREC2000, XNUMBER, XSTRUCT, XNAME, XMALE, XINTCODE
         title( tr("No."), tr("Structure"), tr("Name"),  tr("Male"), tr("Int") ) where XNUMBER <> notappl );
    endif;
  endif;
