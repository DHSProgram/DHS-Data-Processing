{Application 'SEGMENTATION' logic file generated by CSPro }
PROC GLOBAL

  numeric xcluster, xintnum, n, i, j, entries, selseg, hhsegment, xtothh, segmendone, x;

  { setup basic user bar }
  function userbase();
    userbar( clear );
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );
    userbar( add button, ">>|",  do("AdvanceToEnd") );
    userbar( add button, "Lang", do("ChangeLanguage") );
  end;

  { set value sets based on language }
  function OnChangeLanguage()
    SetLanguage( getlanguage() );
  end;

PROC FL_CNULL
preproc

  xcluster = tonumber( sysparm()[1:4] );
  xintnum  = tonumber( sysparm()[5:4] );

  setfont( ValueSets, "Arial", 24, bold );
  SetLanguage( getlanguage() );

  setproperty(SEGMENT_DCT, "ShowExtendedControlTitle", "No");  

  { set up minimal user bar }
  userbase();
  userbar( show );
  enter SEGMENT_FF;
  stop(1);

PROC SEGMENT_FORM
preproc
  SCLUSTER   = xcluster;
  seed( SCLUSTER );
  hhsegment = 0;
  loadcase( SEGMENT_DCT, SCLUSTER );

postproc
  if hhsegment then
    writecase( SEGMENT_DCT );
    close( SEGMENT_DCT );
  endif;

PROC SOPTION
onfocus
  segmendone = ( visualvalue( SSEGNUM ) <> notappl );

postproc
  LCLUSTER = xcluster;
  LINTNUM  = xintnum;
  if SOPTION in 1,notappl & loadcase( HHLISTING, LCLUSTER, LINTNUM ) & soccurs(RECORD2) > 1 then
    errmsg( 60065, SCLUSTER );
    reenter;
  endif;
  if $ = 1 & visualvalue( SSEGNUM ) <> notappl then
    x = accept( tr("Segment for cluster was already selected.  Do you want to select it again?"), tr("Yes"), tr("No") );
	if x = 1 then
      errmsg(  60064 );
    else
      advance
    endif;
  endif;
  do i = 1 while visualvalue(SEGNUM(i)) <> notappl
  enddo;
  if $ = 1 & visualvalue(SHHNUMB(1)) = notappl then
    advance SEGNUM(1)
  elseif $ = 1 & visualvalue(SEGNUM(i)) = notappl then
    { do nothing }
  elseif visualvalue(SHHNUMB(1)) <> notappl then
    advance
  else
   endsect
  endif;

PROC SEGNUM
preproc
  $ = curocc();

PROC SCUMMUL
preproc
  if SEGNUM = 1 then
    $ = SHHNUMB;
  else
    $ = $(SEGNUM-1) + SHHNUMB;
  endif;

PROC SPERCENT
preproc
  if visualvalue($(SEGNUM)) = notappl | SOPTION = 1 then
    $ = 0;
  endif;

postproc
  if SMORE <> 1 then
    endsect
  endif;

PROC SAUXILIAR
  if $ = 1 & totocc( SEGMENT000 ) <= 1 then
    errmsg( 60061 );
    reenter
  elseif $ <> 1 & visualvalue(STOTHH) = notappl then
    errmsg( 60062 );
    skip to SFINAL
  elseif $ <> 1 & visualvalue(STOTHH) <> notappl then
    advance; advance;
  elseif SOPTION = 1 & $ <> 1 then
    errmsg( 60060, SCLUSTER );
    reenter;
  endif;
  if $ = 1 & SOPTION in 1,notappl then
    entries = totocc(SEGMENT000);
    xtothh  = SCUMMUL(entries);
    for i in SEGMENT000 do
      SPERCENT = SHHNUMB * 100 / xtothh;
    enddo;
    selseg = random( 1, xtothh );
    for i in SEGMENT000 do
      if SCUMMUL > selseg then
        break;
      endif;
    enddo;
    SSEGNUM = i;
    SSEGHH  = SHHNUMB(i);
    STOTHH  = xtothh;
  endif;

PROC STOTHH
  hhsegment = SSEGHH;

PROC SFINAL
preproc
  if SAUXILIAR = 2 then
    endsect
  endif;
