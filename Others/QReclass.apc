{Application 'QURECLAS' logic file generated by CSPro }
PROC GLOBAL

  numeric quest, i, j, reclass;

  string wothques;
  array codequest(500,100);
  array string alphques(500);

PROC XOTHERS_FF
preproc

  wothques = " ";
  quest    = 0;
  do i = 1 while i <= 500 by 1
    alphques(i) = " ";
    do j = 1 while j <= 100 by 1
      codequest(i,j) = 0;
    enddo;
  enddo;

postproc

  do i = 1 while i <= quest by 1
    write( " " );
    write( "Reclassifications/New codes for Question: %s", alphques(i) );
    write( "----------------------------------------------------");
    write( "Code   Number of cases" );
    reclass = 0;
    do j = 1 while j <= 100 by 1
      if codequest(i,j) > 0 then
        write( "%3d  %10d", j, codequest(i,j) );
        reclass = 1;
      endif;
    enddo;
    if !reclass then
      write( "No RECLASSIFICATIONS done for this question" );
    endif;
    write(" ");
  enddo;

PROC LEVEL_1

  if compare( wothques, XOTHQUES ) then
    quest = quest + 1;
    alphques(quest) = XOTHQUES;
    wothques = XOTHQUES;
  endif;
  if XOTHCODE > 100 then XOTHCODE = 100 endif;
  if XOTHCODE > 0 then
    codequest(quest, XOTHCODE) = codequest(quest,XOTHCODE) + XOTHCNT;
  endif;
