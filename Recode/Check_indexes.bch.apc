{Application 'CHECK_INDEXES' logic file generated by CSPro}
PROC GLOBAL
numeric i, x;

PROC RECODE8_FF

PROC WOMAN
if V015 = 1 then

  { pregnancy history }
  for i in REC22_EDT do
    if P16 in 1:99 then
	  if P5 <> 1 then
	    errmsg("Pregnancy %d: P16=%d Child not alive (P5=%d)", i, P16, P5);
	  endif;
	  if HV104(P16) <> P4 then
	    errmsg("Pregnancy %d: P16=%d Sex in household (%l) different from sex in birth history (%l)", i, P16, HV104(P16), P4);
	  endif;
	  if abs(HV105(P16) - P8) > 2 then
	    errmsg("Pregnancy %d: P16=%d Age in household (%v) more than 2 years different from age in birth history (%v)", i, P16, HV105(P16), P8);
	  endif;
	endif;
  enddo;
  for i in REC21_EDT do
	x = seek(PIDXB = BIDX);
    if P32(BIDXP) = 1 then
	  if !x then
	    errmsg("REC22 - entry missing for BIDX = %d", BIDX);
	  endif;
	elseif x then
	  errmsg("REC22 - invalid entry found for BIDX = %d", BIDX);
	endif;
  enddo;
  
  { birth history }
  for i in REC21_EDT do
    if B16 in 1:99 then
	  if B5 <> 1 then
	    errmsg("Birth %d: B16=%d Child not alive (B5=%d)", i, B16, B5);
	  endif;
	  if HV104(B16) <> B4 then
	    errmsg("Birth %d: B16=%d Sex in household (%l) different from sex in birth history (%l)", i, B16, HV104(B16), B4);
	  endif;
	  if abs(HV105(B16) - B8) > 2 then
	    errmsg("Birth %d: B16=%d Age in household (%v) more than 2 years different from age in birth history (%v)", i, B16, HV105(B16), B8);
	  endif;
	endif;
  enddo;
  for i in REC22_EDT do
	x = seek(BIDXP = PIDX);
    if P32 = 1 then
	  if !x then
	    errmsg("REC21 - entry missing for PIDX = %d", PIDX);
	  endif;
	elseif x then
	  errmsg("REC21 - invalid entry found for PIDX = %d", PIDX);
	endif;
  enddo;
  
  { maternal care }
  for i in REC41_EDT do
    if !P32(MIDXP) in 1,2 <=> M3A <> notappl then // applicable for live births and stillbirths
	  errmsg("REC41 %d: MIDXP=%d Pregnancy outcome=%d M3A=%d", i, MIDXP, P32(MIDXP), M3A);
	elseif !P32(MIDXP) in 1 <=> M18 <> notappl then // applicable for live births only
	  errmsg("REC41 %d: MIDXP=%d Pregnancy outcome=%d M18=%d", i, MIDXP, P32(MIDXP), M18);
	else
	  if !(MIDX in 1:6 <=> P32(MIDXP) = 1) then
	    errmsg("REC41 %d: MIDXP=%d MIDX=%d", i, MIDXP, MIDX);
		endif;
	  endif;
  enddo;
  for i in REC22_EDT do
	x = seek(MIDXP = PIDX);
    if P19 < 36 then
	  if !x then
	    errmsg("REC41 - entry missing for PIDX = %d", PIDX);
	  endif;
	elseif x then
	  errmsg("REC41 - invalid entry found for PIDX = %d", PIDX);
	endif;
  enddo;

  { vaccinations }
  for i in REC43_EDT do
	if B5(HIDX) = 1 <=> H1 = notappl then
	  errmsg("REC43 %d: HIDX=%d B5=%d H1=%d", i, HIDX, B5(HIDX), H1);
	endif;
  enddo;
  for i in REC21_EDT do
	x = seek(HIDX = BIDX);
    if B19 < 36 & B5 = 1 then
	  if !x then
	    errmsg("REC43 - entry missing for BIDX = %d", BIDX);
	  endif;
	elseif x then
	  errmsg("REC43 - invalid entry found for BIDX = %d", BIDX);
	endif;
  enddo;

  { child health }
  for i in REC4A_EDT do
	if B5(HIDXA) = 1 <=> H11 = notappl then
	  errmsg("REC4A %d: HIDXA=%d B5=%d H11=%d", i, HIDXA, B5(HIDXA), H11);
	endif;
  enddo;
  for i in REC21_EDT do
	x = seek(HIDXA = BIDX);
    if B19 < 60 & B5 = 1 then
	  if !x then
	    errmsg("REC4A - entry missing for BIDX = %d", BIDX);
	  endif;
	elseif x then
	  errmsg("REC4A - invalid entry found for BIDX = %d", BIDX);
	endif;
  enddo;

  { height and weight }
  for i in REC44_EDT do
	if B5(HWIDX) <> 1 then
	  errmsg("REC44 %d: HWIDX=%d B5=%d HW1=%d", i, HWIDX, B5(HWIDX), HW1);
	endif;
  enddo;
  for i in REC21_EDT do
	x = seek(HWIDX = BIDX);
    if B19 < 60 & B5 = 1 then
	  if !x then
	    errmsg("REC44 - entry missing for BIDX = %d", BIDX);
	endif;
	elseif x then
	  errmsg("REC44 - invalid entry found for BIDX = %d", BIDX);
	endif;
  enddo;
  
endif;
