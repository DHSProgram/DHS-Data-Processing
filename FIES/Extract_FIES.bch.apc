{Application 'EXTRACT_FIES' logic file generated by CSPro}
PROC GLOBAL
file FIESfile;

function recode_FIES(xvar)
  numeric newvar = notappl;
  if xvar = 1 then
    newvar = 1
  elseif xvar = 2 then
    newvar = 0
  endif;
  recode_FIES = newvar;
end;  

PROC CCIQ81_FF
preproc
setfile(FIESfile, ".\FIES.dat");

set behavior() export (R);

PROC HOUSEHOLD
preproc
if AHRESULT <> 1 then skip case; endif;

// Construct the variables needed by the R code
FCLUSTER = AHCLUST;
FHNUMBER = AHNUMBER;
URBANRURAL = AHTYPE;
WEALTH   = AHWLTHI;
DEJURE   = count(AHSEC01_EDT where AH05=1);
STRATA   = AHSTRATA;
WT       = AHWEIGHT/1000000;

// Keep the variable names on the left exactly as writen here
WORRIED = recode_FIES(AHFS1);
HEALTHY = recode_FIES(AHFS2);
FEWFOOD = recode_FIES(AHFS3);
SKIPPED = recode_FIES(AHFS4);
ATELESS = recode_FIES(AHFS5);
RUNOUT  = recode_FIES(AHFS6);
HUNGRY  = recode_FIES(AHFS7);
WHLDAY  = recode_FIES(AHFS8);

// If you need to add another background variable, you will need to make changes to the R code. Talk to Trevor
export to FIESfile case_id( FCLUSTER, FHNUMBER), AHREGION, FIES_REC;

freq include(WORRIED,HEALTHY,FEWFOOD,SKIPPED,ATELESS,RUNOUT,HUNGRY,WHLDAY,AHREGION,URBANRURAL,WEALTH,DEJURE,STRATA) title("Unweighted");

freq include(WORRIED,HEALTHY,FEWFOOD,SKIPPED,ATELESS,RUNOUT,HUNGRY,WHLDAY,AHREGION,URBANRURAL,WEALTH,DEJURE,STRATA) title("Weighted by population") weight(wt*dejure);
