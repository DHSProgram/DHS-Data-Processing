{Application 'EXTTEST' logic file generated by CSPro }
PROC GLOBAL
  set explicit;

  numeric pctneg, nextneg, incneg, sampneg, selneg;
  numeric pctpos, nextpos, incpos, samppos, selpos;
  numeric samptot, sampfin, sampinc, sampprv;

PROC TESTLOG_FF
preproc

  pctneg = tonumber( sysparm()[1:3] );  { percentage of negatives to be selected }
  pctpos = tonumber( sysparm()[4:3] );  { percentage of positives to be selected }
  incneg = int( 100/pctneg );           { negative samples to be selected out of 100 }
  incpos = int( 100/pctpos );           { positive samples to be selected out of 100 }
  seed( sysdate("MMDD") );
  nextneg = random( 1, incneg );        { first negative case to be selected }
  nextpos = random( 1, incpos );        { first positive case to be selected }
  sampneg = 0;                          { total negative samples }
  selneg  = 0;                          { negative samples selected }
  samppos = 0;                          { total positive samples }
  selpos  = 0;                          { positive samples selected }
  sampfin = 0;                          { samples with final result }
  sampinc = 0;                          { samples with protocol incomplete }
  sampprv = 0;                          { samples previously included for QC }
  samptot = 0;                          { all samples }

postproc
  write( " " );
  {+US}
  write( "Summary of DBS Samples selected for external QC" );
  write( "-----------------------------------------------" );
  write( " " );
  write( "Samples logged in (scanned)         %6d", samptot );
  write( "Samples with final result           %6d", sampfin );
  write( "Samples previously included for QC  %6d", sampprv );
  write( "Samples with protocol incomplete    %6d", sampinc );
  write( " " );
  write( "Samples analyzed and selected for external retesting" );
  write( ".......Total positives %6d Selected %6d", samppos, selpos );
  write( ".......Total negatives %6d Selected %6d", sampneg, selneg );
  {US+}
  {{SP}
  write( "Resumen de muestras de HIV para control externo" );
  write( "-----------------------------------------------" );
  write( " " );
  write( "Muestras ingresadas (scaneadas)          %6d", samptot );
  write( "Muestras con resultado final             %6d", sampfin );
  write( "Muestras previamente incluidas para CE   %6d", sampprv );
  write( "Muestras con protocolo incompleto        %6d", sampinc );
  write( " " );
  write( "Muestras analizadas/seleccionadas para control externo" );
  write( ".......Total positivas %6d Seleccionadas %6d", samppos, selpos );
  write( ".......Total negativas %6d Seleccionadas %6d", sampneg, selneg );
  {SP}}
  {{FR}
  write( "Récapitulatif des échantillons DBS sélectionnés pour CQ externe" );
  write( "---------------------------------------------------------------" );
  write( " " );
  write( "Echantillons entrés (scannés)            %6d", samptot );
  write( "Echantillons avec résultat final         %6d", sampfin );
  write( "Échantillons précédemment inclus pour CQ %6d", sampprv );
  write( "Echantillons avec protocole incomplet    %6d", sampinc );
  write( " " );
  write( "Échantillons analysés et sélectionnés pour le test externe" );
  write( ".......Total positifs %6d sélectionnés %6d", samppos, selpos );
  write( ".......Total négatifs %6d sélectionnés %6d", sampneg, selneg );
  {FR}}


PROC LEVEL1
  samptot = samptot + 1;               { total DBS samples }
  if samptot = 1 then
    {+US}
    write( "DBS Samples selected for external quality Control (QC)" );
    write( "Lab-ID  Bar Code" );
    {US+}
    {{SP}
    write( "Muestras seleccionadas para control de calidad externo" );
    write( "Lab-ID  Codigo de Barra");
    {SP}}
    {{FR}
    write( "Echantillons DBS sélectionnés pour contrôle de qualité externe (CQ)" );
    write( "Lab-ID  Code barres" );
    {FR}}

    write( "------------------------------------------------------" );
    write( " " );
  endif;

  if LSTAGE in 9 then                  { protocol completed }
    sampfin = sampfin + 1;
    if LEXT = notappl then             { sample never incuded in QC }
      LEXT = 0;
      if LFINRES = 0 then              { analyze negative samples }
        sampneg = sampneg + 1;
        if sampneg = nextneg then
          LEXT    = 1;
          selneg  = selneg + 1;
          nextneg = nextneg + incneg;
          write( "%6d    %s    Negative", LABID, LBAR );
        endif;
      elseif LFINRES in 1,2,3 then     { analyze positive samples }
        samppos = samppos + 1;
        if samppos = nextpos then
          LEXT    = 1;
          selpos  = selpos + 1;
          nextpos = nextpos + incpos;
          write( "%6d    %s    Positive", LABID, LBAR );
        endif;
      endif;
    else                              { sample previuosly included in QC }
      sampprv = sampprv + 1;
    endif;
  else                                { count samples with protocol incomplete }
    sampinc = sampinc + 1;
  endif;
