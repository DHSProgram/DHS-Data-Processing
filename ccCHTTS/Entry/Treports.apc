{Application 'TreptypeS' logic file generated by CSPro }
PROC GLOBAL

  set explicit;

  alpha(18)  labelfin;
  alpha(120) header;
  alpha(120) linehd;
  alpha(8)   pnstr;
  alpha(3)   ynstr;
  alpha(1)   v1T, v2T, m1t, m2T, geen, wplab, wlock, wret;
  string wplate;

  numeric reptype, linecount, pagecount, maxlines, ok;
  numeric i, v1, m1, w1, v2, m2, w2;
  numeric t1, t2, t3, t4, t5, t6, t7, t8, t9, t10, t11, t12, t13, t14, t15, t16;
  string dispname = ".\treports.html";

PROC TESTLOG_FF
preproc

  // set up html file for new cluster number
  // htm_init_styles(strip(dispname),"TablesStyle");
  htm_init_styles(strip(dispname),"TablesStyle");
  htm_hdr("EDS REPUBLIQUE DEMOCRATIQUE DU CONGO TEST VIH",1 );
  pnstr  = "NPPP   I";   { N-Negative, P-positive, I-Indeterminat }
  ynstr  = " YN";

  htm_tabinit("");
  htm_tabhdr(  "ID|Bar|Cur.Plate|Locked|RECVD|M1|RETEST|G1|M2|G2|Geenius|TESTING STAGE|FINAL RESULT|" );

  { reptype to show : 0 : summary stats, 1: all, 2: no final result, 3-6 : levels 1-4 }
  reptype   = tonumber( sysparm()[1:1] );
  linecount = 1;
  pagecount = 1;
  maxlines  = 65;  { # lines in a page }
  write( "%s", strip(header) );
  write( "%s", strip(linehd) );

postproc

  htm_tabclose();
  htm_par(" ");
  htm_tabinit("");
  htm_hdr( "Testing protocol summary",2 );
  htm_tabhdr("Description|Number|");
  htm_tabrow  ( maketext( "Total samples scanned:                           |%6d|", t1 ) );
  htm_tabrow  ( maketext( "Positives first level:                           |%6d|", t2 ) );
  htm_tabrow  ( maketext( "Positives first and second levels:               |%6d|", t3 ) );
  htm_tabrow  ( maketext( "Positives first level, negatives second level:   |%6d|", t4 ) );
  htm_tabrow  ( maketext( "In grey area in first level:                     |%6d|", t14  ));
  htm_tabrow  ( maketext( "In grey area first level, positive second level: |%6d|", t15  ));
  htm_tabrow  ( maketext( "In grey area first level, negative second level: |%6d|", t16  ));
  htm_tabrow  ( maketext( "Negative samples first level:                    |%6d|", t5 ) );
  htm_tabrow  ( maketext( "Negatives first level, positives second level:   |%6d|", t6 ) );
  htm_tabrow  ( maketext( "Negatives first and second level:                |%6d|", t7 ) );
  htm_tabrow  ( maketext( "Still pending for testing at first level:        |%6d|", t8 ) );
  htm_tabrow  ( maketext( "Selected for test with Geenius:                  |%6d|", t9 ) );
  htm_tabrow  ( maketext( "Confirmed positive samples:                      |%6d|", t10  ));
  htm_tabrow  ( maketext( "Confirmed negative samples:                      |%6d|", t11  ));
  htm_tabrow  ( maketext( "Indeterminat/not enough DBS, no final result yet:|%6d|", t12  ));
  htm_tabrow  ( maketext( "Samples selected for external QC:                |%6d|", t13  ));

  htm_close();
  htm_disp(dispname);

PROC LEVEL1
preproc

  { initialize working variables }
  v1 = notappl; m1 = notappl; w1 = notappl;
  v2 = notappl; m2 = notappl; w2 = notappl;
  v1T   = " ";        { level 1 text, ie, Verinostika }
  v2T   = " ";        { level 2 text, ie, Genscreen }
  m1T   = " ";        { level 3 text repeat, ie, Verinostika }
  m2T   = " ";        { level 3 text repeat, ie, Genscreen }
  geen = " ";        { level 4 text, ie, Western blot }
  wplab = " ";        { level 4 text, ie, Pepti-lab }
  wlock = " ";        { whether sample was blocked }
  wret  = " ";        { whether sample selected for internal QC }

  do i = 1 while i <= totocc(LTEST000)
    if LTYPE(i) = 1 & LCANCEL(i) <> 1 then v1 = LTRES(i) endif;   { level 1, ie, Verinostika }
    if LTYPE(i) = 2 & LCANCEL(i) <> 1 then m1 = LTRES(i) endif;   { level 2, ie, Genscreen   }
    if LTYPE(i) = 3 & LCANCEL(i) <> 1 then v2 = LTRES(i) endif;   { level 3 repeat, ie, Verinostika }
    if LTYPE(i) = 4 & LCANCEL(i) <> 1 then m2 = LTRES(i) endif;   { level 3 repeat, ie, Genscreen   }
  enddo;

  { count totals for summary report }
  t1 = t1 + 1;              { total samples scanned }
  if v1 = 1 then
    t2 = t2 + 1;            { positives first level }
    if m1 = 1 then
      t3 = t3 + 1;          { positives first and second levels }
    elseif m1 = 0 then
      t4 = t4 + 1           { positives first level, negatives sencond level }
    endif;
  elseif v1 = 0 & LRET = 2 then
    t14 = t14 + 1;          { samples in grey area in first level }
    if m1 = 1 then
      t15 = t15 + 1;        { grey area first level, positives second level }
    elseif m1 = 0 then
      t16 = t16 + 1;        { grey area first level, negative second level }
    endif;
  elseif v1 = 0 then
    t5 = t5 + 1;            { negative samples first level }
    if m1 = 1 then
      t6 = t6 + 1;          { negatives first level, positives second level }
    elseif m1 = 0 then
      t7 = t7 + 1;          { negatives first and second level }
    endif;
  else
    t8 = t8 + 1;            { samples still pending for first level }
  endif;
  if LDETERM1 in 0:1,7 | LSTAGE = 4 then
    t9 = t9 + 1;            { selected for test with western blot }
  endif;
  if LFINRES in 1:3 then
    t10 = t10 + 1;          { total positive samples }
  elseif LFINRES = 0 then
    t11 = t11 + 1;          { total negative samples }
  else
    t12 = t12 + 1;          { indeterminat/not enough DBS, no final result yet }
  endif;
  if LEXT = 1 then
    t13 = t13 + 1;          { samples selected for external QC }
  endif;

  { skip samples according to the selected report }
  recode reptype :: LSTAGE :: LEXT -> ok;
         0,1  ::        ::      -> 1;      { all samples }
           2  :: 1-4    ::      -> 1;      { awaiting any test result }
           3  :: 1      ::      -> 1;      { awaiting Murex }
           4  :: 2      ::      -> 1;      { awasiting Genscreen }
           5  :: 3      ::      -> 1;      { awaiting repeat test }
           6  :: 4      ::      -> 1;      { awaiting geenius }
           7  ::        ::   1  -> 1;      { samples for external QC }
              ::        ::      -> 0;
  endrecode;
  if !ok then skip case; endif;

  { convert numeric results to P/N Y/N etc }
  if v1 in 0,1 then v1T = pnstr[v1+1:1] endif;
  if v2 in 0,1 then v2T = pnstr[v2+1:1] endif;
  if m1 in 0,1 then m1T = pnstr[m1+1:1] endif;
  if m2 in 0,1 then m2T = pnstr[m2+1:1] endif;
  if LDETERM1 in 0:1,7 then geen = pnstr[LDETERM1+1:1] endif;
  if LDETERM2 in 1:3,7 then wplab = pnstr[LDETERM1+1:1] endif;
  if !special(LFINRES) then
    labelfin = GetLabel( LFINRES, LFINRES );
  else
    labelfin = "No final result";
  endif;
  if LRET  in 0:2 then wret = ynstr[LRET+1:1] endif;
  if LLOCK in 0,1 then wlock = ynstr[LLOCK+1:1] endif;
  numeric tests = noccurs(LTEST);
  if tests then
    wplate = LPLATE(tests);
    if LCANCEL(tests) then
      wplate = wplate + "!";
    endif;
  else
    wplate = "";
  endif;
  { format should agree for the most part }
  htm_tabrow(maketext 
        ( 
           " %05d|%5s|%s|%1s|%8d|%2s|%5s|%6s|%3s|%4s|%3s|%32s|%18s|",
           LABID, LBAR, wplate, wlock, LDATE, v1T, wret, m1T, v2T, m2T, geen, LSTAGET, labelfin )
        );
  linecount=linecount+1;

  if linecount = maxlines then
    linecount = 1;
    pagecount = pagecount + 1;
    write( "%s", strip(linehd) );
    write( "                                         Page %3d", pagecount );
    write( "%s", strip(header) );
    write( " " );
  endif;
