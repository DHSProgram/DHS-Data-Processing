{Application 'InputWgt8' logic file generated by CSPro }
PROC GLOBAL

  { !!! Make sure to include a valid range of value sets to variable AHCLUST   }
  {     i,e, 1-720. if not provided a displacement in InputWgt.csv file occurs }

  numeric i, xcluster, hhfound;

  crosstab float(0) tw1 AHREGION+total coltw
    title( "Results by region for all questionnaires for input to weights, Country 2020" )
    exclude( totals, percents, specval );

  crosstab float(0) tw2 AHCLUST+total coltw
    title( "Table with complete interviews to provide input for the calculation",
           "of weights for: Households, households selected for male interview,",
           "women, men, HIV for women and HIV for men, Country 2020" )
    exclude( totals, percents, specval );

  crosstab float(0) tw3 AHCLUST+total domviol
    title( "Domestic violence for input to weights,",
           "eligible women, Country 2020" )
    exclude( totals, percents, specval );

  crosstab float(0) tw4 AHCLUST+total domviol
    title( "Domestic violence for input to weights,",
           "complete interviews, Country 2020" )
    exclude( totals, percents, specval );
	
  file csv1, csv2; 
	
PROC CCIQ81_FF
preproc

  xcluster = 0;
  total    = 0;

postproc 

  numeric rowmax = tblrow(tw2); // total clusters (total row counted but will not be output) 
  numeric r,j; 
  string csvrow; 
  
  setfile(csv1, "InputWgt1.csv", create); 
  setfile(csv2, "InputWgt2.csv", create); 

  // Loop through all clusters 
  do i = 0 while i <= rowmax // 0 for header row, 1 - rowmax for the clusters. 
  
    // CSV1 - households, women, men 
    if i = 0 then // output header to CSV 
      csvrow = "Cluster"; 
      do j = 0 while j <= tblcol(tw2); 
        csvrow = csvrow + "," + getlabel(coltw,j); 
      enddo; 
    else // cluster data 
      csvrow = maketext("%04d",i); 
      r = i - 1; 
      do j = 0 while j <= tblcol(tw2); 
        csvrow = csvrow + maketext(",%d",tw2(r,j)); 
      enddo; 
    endif; 
    filewrite(csv1, csvrow); 
    
    // CSV2 - domestic violence 
    if i = 0 then // output header to CSV 
      csvrow = "Cluster"; 
      do j = 0 while j <= tblcol(tw3); 
        csvrow = csvrow + "," + getlabel(domviol,j+1); 
      enddo; 
      do j = 0 while j <= tblcol(tw4); 
        csvrow = csvrow + "," + getlabel(domviol,j+1); 
      enddo; 
    else // cluster data 
      csvrow = maketext("%04d",i); 
      do j = 0 while j <= tblcol(tw3); 
        csvrow = csvrow + maketext(",%d",tw3(r,j)); 
      enddo; 
      do j = 0 while j <= tblcol(tw4); 
        csvrow = csvrow + maketext(",%d",tw4(r,j)); 
      enddo; 
    endif; 
    filewrite(csv2, csvrow); 

  enddo; 
  

PROC HOUSEHOLD
  { households found include the following codes in variable AHRESULT:
      1-complete,
      2-not household member at home or no competent respondent,
      4-postponed
      5-refused
      8-dwelling not found
    households excluded according to AHRESULT are:
      3-Entire household absent for extended period of time
      6-dwelling vacant or address not a dwelling
      7-dwelling destroyed
      9-other
  }
  hhfound = ( AHRESULT in 1,2,4,5,8 );

  { tables tw1 & tw2 }
  if xcluster <> AHCLUST then
    coltw = 0;                    { count clusters in each region }
    xtab( tw1 );
    xcluster = AHCLUST;
  endif;
  if hhfound then
    { all households found }
    coltw = 1;                          { households found }
    xtab( tw1 );
    xtab( tw2 );
    if AHRESULT = 1 then
      coltw = 2;                        { households complete }
      xtab( tw1 );
      xtab( tw2 );
    endif;
    { households found for male sub-sample }
    if AHELIGM = 1 then                 { eligible for men interview }
      coltw = 5;                        { households found }
      xtab( tw1 );
      xtab( tw2 );
      if AHRESULT = 1 then
        coltw = 6;                      { households complete }
        xtab( tw1 );
        xtab( tw2 );
      endif;
    endif;
  endif;

PROC INDIVIDUAL
preproc

  if AH06( ALINE ) <> 1 then skip case; endif;   { skip none de facto population }

  { tables tw1 and tw2 }
  { all de facto individuals are eligible regardless of the result of the inteview }
  if AQTYPE = 2 then       { women questionnaire }
    coltw = 3;
    xtab( tw1 );
    xtab( tw2 );
    if ARESULT = 1 then    { women complete interview }
      coltw = 4;
      xtab( tw1 );
      xtab( tw2 );
    endif;
  else                     { men questionnaire }
    coltw = 7;
    xtab( tw1 );
    xtab( tw2 );
    if ARESULT = 1 then    { men complete interview }
      coltw = 8;
      xtab( tw1 );
      xtab( tw2 );
    endif;
  endif;

  { table tw3, domestic violence }
  { de facto woman, selected for domestic violence and with a complete interview }
  domviol = count( AHSEC01 where AH06 = 1 & AH09 <> 0 );
  if domviol & ALINE = AHNUMDV & ARESULT = 1 then
    if domviol > 5 then domviol = 5 endif;
    xtab( tw3 );
    if DV01 = 1 then
      xtab( tw4 );
    endif;
  endif;
