{Application 'FieldWKcheck' logic file generated by CSPro}
PROC GLOBAL
  
  numeric i, fw, n_i, n_fw, x;

  // Variables from Interv dataset
  array i_code(900);
  array i_team(900);
  array i_role(900);
  array i_sex (900);
  array i_used(900); // whether the code is used in the IQ file
  array i_err (900); // numbers of errors for this code based on sex
  array i_sex_int(900,2); // numbers of men (1) and women (2) interviewed

  // Variables from FW dataset
  array fw_code(900);
  array fw_sex (900); 
  array fw_used(900); // whether the code is used in the IQ file
  array fw_err (900); // numbers of errors for this code based on sex
  array fw_sex_int(900,2); // numbers of men (1) and women (2) interviewed


  function find_i(ix)
    // Find the interviewer code in the Interv data
    do i = 1 while i <= n_i & ix <> i_code(i) 
    enddo;
    if i > n_i then
      i = 0; // Code not found in Interv data
    else
      i_used(i) = 1;	// Interv data fieldworker code used in questionnaires
    endif;
    find_i = i;
  end;

  function find_fw(fx)
    // Find the interviewer code in the Fieldworker data
    do fw = 1 while fw <= n_fw & fx <> fw_code(fw) enddo;
    if fw > n_fw then
      // fw = 0; // code not found in Fieldworker data
      fw_code(fw) = fx;
      fw_used(fw) = (-1);
      n_fw = fw;
      fw = (-fw);
    elseif fw_used(fw) < 0 then // Negative value means fieldworker missing from file, but added due to earlier case
      fw_used(fw) = fw_used(fw)-1;  // 
      fw = (-fw);	// make negative in the return value, meaning not in fieldworker file
    else
      fw_used(fw) = 1;	// Fieldworker data fieldworker code used in questionnaires
    endif;
    find_fw = fw;
  end;

  function chkfw(fx,fwrole,fwsex);
    // fx - fieldworker code
    // fwrole - expected role of the fieldworker - 0 no particlar role expected. 1=Interviewer, 2=Supervisor, 3=Health tech, 4=Field editor
    // fwsex - expected sex of the fieldworker (when interviewing women or men)
  
    string role = getlabel(irole,fwrole);          // Label for the role 
    if fwrole = 0 then role = "Fieldworker" endif; // Role is undefined 
    // adjust code to handle different roles
    if fwrole = 3 then 
      role = "Measurer"; 
      fwrole = 0; // If measurers are supevisors and interviewers
    endif; 
    if fwrole = 4 then 
      role = "Field editor"; 
    endif; 

    // Check Interv data
    if fx <> notappl then  // Ignore if the fieldworker code is notappl (for some biomarker cases that were not actually measured) }
      i  = find_i (fx);	 // Find the entry in the Interv tables
      if n_i then          // If the Interv table does not exist, then don't do these checks
        if i = 0 then		 // Fieldworker code does not exist in the Interv data
          // if fwrole = 3 or fwrole = 4 then
          //   OK if measurers and field editors are not in interv.dat
          // else
            errmsg("%s %d not found in Interv data",role,fx);
          // endif;
        else               // Fieldworker code does exist in Interv data
          if fwrole & fwrole <> i_role(i) then  // but a particular role is expected and the role doesn't match
            errmsg("%s %d was a %s in Interv data",role,fx,getlabel(irole,i_role(i)));
          endif;       
          inc(i_sex_int(i,fwsex));
          if fwsex & fwsex <> i_sex(i) then  // a particular sex is expected and the sex does not match
            errmsg("Sex of %s %d is %s in Interv data, but interviewed a %s",role,fx,getlabel(isex,i_sex(i)),getlabel(AQTYPE,fwsex));
            inc(i_err(i));
          endif;
        endif;
      endif;

      fw = find_fw(fx);    // Find the entry in the Fieldworker tables
      if fw <= 0 then       // Fieldworker code does not exist in the Fieldworker data
        errmsg("%s %d not found in Fieldworker data",role,fx) summary;
      else
        inc(fw_sex_int(i,fwsex));
        if fwsex & fwsex <> fw_sex(fw) then  // a particular sex is expected and the sex does not match
          errmsg("Sex of %s %d is %s in Fieldworker data, but interviewed a %s",role,fx,getlabel(isex,fw_sex(fw)),getlabel(AQTYPE,fwsex));
          inc(fw_err(fw));
        endif;
      endif;
    endif;
  end;

PROC CCIQ81_FF
preproc
  // Load all entries from the Interv data
  while !fileempty(filename(interv)) & loadcase(interv) do
    inc(i);
    i_code(i) = icode;
    i_team(i) = iteam;
    i_role(i) = irole;
    i_sex (i) = isex;
  enddo;
  n_i = i;

  // Load all entries from the Fieldworker data 
  while loadcase(fieldwkq) do
    inc(fw);
    fw_code(fw) = fw101;
    fw_sex (fw) = fw105;
    i = find_i(fw101);  // Find the fieldworker code from the Fieldworker data in the Interv data
    if i = 0 then       // Fieldworker code does exist in Interv data
      errmsg("Fieldworker %d does not exist in Interv data",fw101);
    elseif fw105 <> i_sex(i) then		// Sex in Fieldworker data does not match sex in Interv data
      errmsg("Fieldworker %d sex %s does not match sex %s in Interv data",fw101,getlabel(fw105,fw105),getlabel(isex,i_sex(i)));
    endif;
  enddo;
  n_fw = fw;

  if n_i & n_i <> n_fw then	// Check that the number of Interv data entries matches the number if the Fieldworker data
    errmsg("Number of records in Fieldworker data %d is different from number in Interv data %d",n_fw,n_i);
  endif;

postproc
  do i = 1 while i <= n_i		// Check for any codes in Interv data that are not used
    if i_used(i) = 0 then errmsg("Fieldworker code %d in Interv data is not used",i_code(i)) endif;
    if (i_sex_int(i,1) & i_sex_int(i,2)) | i_err(i) then 
      errmsg("Fieldworker code %d in Interv data interviewed %d men and %d women. Errors=%d",i_code(i),i_sex_int(i,1),i_sex_int(i,2),i_err(i));
    endif;
  enddo;
  do fw = 1 while fw <= n_fw  // Check for any codes in Fieldworker data that are not used
    if fw_used(fw) = 0 then errmsg("Fieldworker code %d in Fieldworker data is not used",fw_code(fw)) endif;
    if fw_used(fw) < 0 then errmsg("Fieldworker code %d not found in Fieldworker data, but used %d times",fw_code(fw),(-fw_used(fw))) endif;
    if (fw_sex_int(fw,1) & fw_sex_int(fw,2)) | fw_err(fw) then 
      errmsg("Fieldworker code %d in Fieldworker data interviewed %d men and %d women. Errors=%d",fw_code(fw),fw_sex_int(fw,1),fw_sex_int(fw,2),fw_err(fw));
    endif;
  enddo;

PROC HOUSEHOLD
preproc
  chkfw(AHSUPERV,2,0);	// Check household supervisor
//!!!  chkfw(AHFEDIT ,4,0);	// Check household field editor if used 
  chkfw(AHINTNUM,1,0);	// Check household interviewer

  {AHFEDIT}
  for x in ABSEC01_EDT do
    chkfw(AB113,3,0);		// Check measurer for children
    chkfw(AB114,3,0);		// Check assistant to measurer for children
    chkfw(AB121,3,0);		// Check hemoglobin measurer for children
  enddo;
  for x in ABSEC02_EDT do
    chkfw(AB209,3,0);		// Check measurer for women
    chkfw(AB210,3,0);		// Check assistant to measurer for women
    chkfw(AB216,3,0);		// Check hemoglobin measurer for women
    chkfw(AB220,3,0);		// Check hemoglobin measurer for women
    chkfw(AB224,3,0);		// Check hemoglobin measurer for women
    chkfw(ABWHIV2,3,0);	// Check health tech for HIV
    chkfw(ABWHIV5,3,0);	// Check health tech for HIV
  enddo;
  for x in ABSEC03_EDT do
    chkfw(AB309,3,0);		// Check measurer for men
    chkfw(AB310,3,0);		// Check assistant to measurer for men
    chkfw(AB316,3,0);		// Check hemoglobin measurer for men
    chkfw(AB320,3,0);		// Check hemoglobin measurer for men
    chkfw(AB324,3,0);		// Check hemoglobin measurer for men
    chkfw(ABMHIV2,3,0);	// Check health tech for HIV
    chkfw(ABMHIV5,3,0);	// Check health tech for HIV
  enddo;

PROC INDIVIDUAL
preproc
  chkfw(ASUPERV,2,0);		// Check supervisor for individual interview
//!!!  chkfw(AFEDIT ,4,0);		// Check field editor for individual interview if used
  chkfw(AINTNUM,1,AQTYPE);	// Check interviewer for individual interview, including by sex
