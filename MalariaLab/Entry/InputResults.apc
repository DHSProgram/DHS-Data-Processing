{Application 'INPUTRESULTS' logic file generated by CSPro }
PROC GLOBAL

  numeric oldval, option, lenvar, isok, nparasit, nstage, i, j;

  string parasite;

  valueset LabTechs, vs;
 
  { to check if more result tests samples will be entered }
  function endmess()
    option = accept(tr("Do you want to enter more test results?"), tr("Yes"), tr("No") );
    endmess = option;
  end;

  { to decide if it is correct to change from valid value to zero (0-not tested yet) }
  function confirm_change()
    option = accept(tr("Are you sure you want to change from a valid result into 'Not analyzed yet'?"), tr("Yes change it"), tr("No") );
    confirm_change = ( option = 1);
  end;

  { check a valid code for the string of parasites enterd }
  function valparasit( string varx )
    isok = 0;
    lenvar = length( strip(varx) );
    if pos( "N", varx ) & lenvar <> 1 then
      isok = 0
    else
      if lenvar then
        isok = 1;
        j    = 1;
        do i = 1 while i <= lenvar
          j = pos( varx[i:1], parasite[j:nparasit]  );
          if !j then
            isok = 0;
            break
          endif;
          j = j + 1;
        enddo;
      endif;
    endif;
    valparasit = isok;
  end;

  { verify if first and second readings are discordant }
  function discordant()
    numeric discord1, discord2, discord3, discord4, pctaden;
    discord1 = ( visualvalue(LRESULT1) in 0,1 & visualvalue(LRESULT2) in 0,1 &
                 visualvalue(LRESULT1) <> visualvalue(LRESULT2) );               { one positive the other negative }
    discord2 = ( visualvalue(LGAMETO1) in 0,1 & visualvalue(LGAMETO2) in 0,1 &
                 visualvalue(LGAMETO1) <> visualvalue(LGAMETO2) );               { gametocytes are different }
    discord3 = ( visualvalue(LRESULT1) in 0,1 & visualvalue(LRESULT2) in 0,1 &
                 LSPECIE1 <> LSPECIE2 );                                         { parasites are different }
    if visualvalue(LRESULT1) = 1 & visualvalue(LRESULT2) = 1 then                { for positives }
      pctaden = (visualvalue(LDENSITY1)-visualvalue(LDENSITY2))*100 / visualvalue(LDENSITY1);
      if abs(pctaden) > 25 then              { asexual density difference is more than 25% }
        discord4 = 1
      endif;
    endif;
//	errmsg("Discord=%d%d%d%d pctaden=%10.3f", discord1, discord2, discord3, discord4, pctaden );
    discordant = ( discord1 | discord2 | discord3 | discord4 );
  end;

PROC CNULL_FF
preproc

  { set font for value sets }
  setfont( ALL, "Arial", 22, bold );
  setproperty(TESTLOG_DICT, "ShowExtendedControlTitle", "No");

  set behavior() exit on;
  set behavior( LABTECH1, LABTECH2, LABTECH3) canenter(notappl) on (noconfirm);

  { set language at start of the program, defaulting to language passed by menu }
  setlanguage( getlanguage() );

  parasite  = "ABCD";
  nparasit  = length( strip(parasite) );

  { load system users as lab technicians }
  LabTechs.clear();
  while loadcase(USERS_DICT) do
    LabTechs.add(strip(UNAME1) + " " + strip(UNAME2), UOPID);
  enddo;


PROC ID
  LABNUMBER = $;
  if !loadcase( LABINDEX, LABNUMBER ) then
    errmsg( 100 );
    if endmess() = 1 then
      reenter;
    else
      stop(1)
    endif;
  else
    LBAR = LBARCODE;
    if !loadcase( TESTLOG_DICT, LBAR ) then
      errmsg( 110, LBAR );
      if endmess() = 1 then
        reenter;
      else
        stop(1)
      endif;
    endif;
    ENTER TESTLOG_FF;
    if endmess() = 1 then
      reenter;
    else
      stop(1)
    endif;
  endif;

PROC LOPTION
onfocus
  vs.clear();
  if special(visualvalue(LRESULT1)) then
    vs.add(tr("Enter results for first reading"), 1)
  else
    vs.add(tr("Modify results for first reading"), 2);
    if special(visualvalue(LRESULT2)) then
      vs.add(tr("Enter results for second reading"), 3)
    else
      vs.add(tr("Modify results for second reading"), 4);
	  if discordant() then
        if special(visualvalue(LRESULT3)) then
          vs.add(tr("Enter results for third reading"), 5)
        else
          vs.add(tr("Modify results for third reading"), 6)
        endif;
	  endif;
    endif;
  endif;
  vs.add(tr("Cancel (return to main menu)"), 9);
  setvalueset(LOPTION, vs);

postproc
  if LOPTION = 1 & visualvalue(LRESULT1) <> missing then
    errmsg( 120 );
    reenter
  elseif LOPTION = 2 & visualvalue(LRESULT1) = missing then
    errmsg( 125 );
    reenter
  elseif LOPTION in 3,4 then
    if visualvalue(LRESULT1) = missing then
      errmsg( 130 );
      reenter
    elseif LOPTION = 3 & visualvalue(LRESULT2) <> missing then
      errmsg( 135 );
      reenter
    elseif LOPTION = 4 & visualvalue(LRESULT2) = missing then
      errmsg( 140 );
      reenter
    endif;
    advance to LRESULT2;
  elseif LOPTION in 5,6 then
    if visualvalue(LRESULT1) = missing | visualvalue(LRESULT2) = missing then
      errmsg( 145 );
      reenter
    elseif LOPTION = 5 & visualvalue(LRESULT3) <> missing then
      errmsg( 150 );
      reenter
    elseif LOPTION = 6 & visualvalue(LRESULT3) = missing then
      errmsg( 155 );
      reenter
    elseif LOPTION = 5 & !discordant() then
      errmsg( 160 );
      reenter
    endif;
    advance to LRESULT3;
  elseif $ = 9 then
    advance to LEND
  endif;

PROC LRESULT1
preproc
  oldval = visualvalue( $ );

postproc
  if LOPTION = 1 then
    if $ = missing then
      errmsg( 200 );
      reenter LOPTION
    endif;
    LSTATUS  = 2;
  elseif LOPTION = 2 & $ = missing then
    if !confirm_change() then
      $ = oldval;
      reenter LOPTION
    endif;
    LSTATUS = 1;
  endif;
  if $ = 6 then 
    skip to LABTECH1
  elseif $ = 0 then 
    skip to LGAMETO1
  elseif $ = missing then
    skip to LRESULT2
  endif;
  
PROC LASEXUAL1
  if $ = 0 then
    errmsg( 201 );
    reenter
  endif;	

PROC LWBCA1
  if LASEXUAL1 < 100 then
    if $ < 500 then
      errmsg( 202 );
      reenter
    endif;
  else	
    if $ < 200 then
      errmsg( 205 );
      reenter
    endif;
  endif;
  
PROC LDENSITY1
preproc
  $ = 8000*LASEXUAL1/LWBCA1;

PROC LSPECIE1
preproc
  if LRESULT1 <> 1 then
    skip to LABTECH1
  endif;

postproc
  if !valparasit( $ ) then
    errmsg( 210 );
    reenter
  endif;

PROC LABTECH1
onfocus 
  SetValueSet( $, LabTechs );
  
postproc
  if $ = notappl then
    errmsg( 220 );
    reenter
  endif;
  
PROC LREMARK1
  if LRESULT1 = 6 & !length(strip($)) then
    errmsg( 245 );
	reenter
  endif;	

PROC LRESULT2
preproc
  oldval = visualvalue( $ );
  if LOPTION in 1,2 then
    advance to LEND
  endif;

postproc
  if $ <> missing then
    if LRESULT1 = 6 <=> $ <> 6 then
      errmsg( 250 );
	  reenter
    endif;	  
  endif;  
  if LOPTION = 3 then
    if $ = missing then
      errmsg( 200 );
      reenter LOPTION
    endif;
    LSTATUS  = 3;
  elseif LOPTION = 4 & $ = missing then
    if !confirm_change() then
      $ = oldval;
      reenter LOPTION
    endif;
    LSTATUS = 2;
  endif;
  if $ = 6 then
    skip to LABTECH2
  elseif $ = 0 then
    skip to LGAMETO2
  elseif $ = missing then
    skip to LRESULT3
  endif;
  
PROC LASEXUAL2
  if $ = 0 then
    errmsg( 201 );
    reenter
  endif;	

PROC LWBCA2
  if LASEXUAL2 < 100 then
    if $ < 500 then
      errmsg( 202 );
      reenter
    endif;
  else	
    if $ < 200 then
      errmsg( 205 );
      reenter
    endif;
  endif;
  
PROC LDENSITY2
preproc
  $ = 8000*LASEXUAL2/LWBCA2;

PROC LSPECIE2
preproc
  if LRESULT2 <> 1 then
    skip to LABTECH2
  endif;

postproc
 if !valparasit( $ ) then
    errmsg( 210 );
    reenter
  endif;

PROC LABTECH2
onfocus 
  SetValueSet( $, LabTechs );
  
postproc
  if $ = notappl then
    errmsg( 220 );
    reenter
  elseif LABTECH1 <> 0 & $ <> 0 & LABTECH1 = $ then
    errmsg( 230 );
    reenter
  endif;
  if !discordant() then
    LSTATUS = 9
  endif;
  
PROC LREMARK2
  if LRESULT2 = 6 & !length(strip($)) then
    errmsg( 245 );
	reenter
  endif;	

PROC LRESULT3
preproc
  oldval = visualvalue( $ );
  if LOPTION in 1:4 then
    advance to LEND
  endif;

postproc
  if LOPTION = 5 then
    if $ = missing then
      errmsg( 200 );
      reenter LOPTION
    endif;
    LSTATUS  = 9;
  elseif LOPTION = 6 & $ = missing then
    if !confirm_change() then
      $ = oldval;
      reenter LOPTION
    endif;
    LSTATUS = 3;
  endif;
  if $ = 0 then
    skip to LGAMETO3
  elseif $ = missing then
    skip to LEND
  endif;
  
PROC LASEXUAL3
  if $ = 0 then
    errmsg( 201 );
    reenter
  endif;	

PROC LWBCA3
  if LASEXUAL3 < 100 then
    if $ < 500 then
      errmsg( 202 );
      reenter
    endif;
  else	
    if $ < 200 then
      errmsg( 205 );
      reenter
    endif;
  endif;
  
PROC LDENSITY3
preproc
  $ = 8000*LASEXUAL3/LWBCA3;

PROC LSPECIE3
preproc
  if LRESULT3 <> 1 then
    skip to LABTECH3
  endif;

postproc
  if !valparasit( $ ) then
    errmsg( 210 );
    reenter
  endif;

PROC LABTECH3
onfocus 
  SetValueSet( $, LabTechs );
  
postproc
  if $ = notappl then
    errmsg( 220 );
    reenter
  elseif LABTECH1 <> 0 & LABTECH2 <> 0 & $ <> 0 & ( LABTECH1 = $ | LABTECH2 = $ ) then
    errmsg( 240 );
    reenter
  endif;

PROC LEND
preproc
  if LOPTION = 9 then
    $ = 3;
    noinput;
  endif;

postproc
  if $ = 1 then
    { establish the test final result }
    recode LRESULT1 :: LRESULT2 :: LRESULT3 -> LFINRES;
               6    ::     6    ::          -> 6;	
                    ::          ::    0,1   -> LRESULT3;	
               1    ::     1    ::          -> 1;	
               0    ::     0    ::          -> 0;	
                    ::          ::          -> notappl;
    endrecode;					
    { update testlog with test result }
    writecase( TESTLOG_DICT );
  elseif $ = 2 then
    reenter LOPTION
  endif;
