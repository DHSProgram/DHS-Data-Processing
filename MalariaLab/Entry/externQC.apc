{Application 'EXTTEST' logic file generated by CSPro }
PROC GLOBAL

  numeric pcteqc, nexteqc, inceqc, sampneg, selneg, samppos, selpos;
  numeric samptot, sampfin, sampinc, sampprv, sampoth, sampeqc;
  string  testres;

PROC TESTLOG_FF
preproc

  pcteqc = tonumber( sysparm()[1:3] );  { percentage to be selected for EQC }
  inceqc = int( 100/pcteqc );           { samples to be selected out of 100 }
  seed( sysdate("MMDD") );
  nexteqc = random( 1, inceqc );        { first sample to be selected }
  sampneg = 0;                          { total negative samples }
  selneg  = 0;                          { negative samples selected }
  samppos = 0;                          { total positive samples }
  selpos  = 0;                          { positive samples selected }
  sampfin = 0;                          { samples with final result }
  sampinc = 0;                          { samples with protocol incomplete }
  sampprv = 0;                          { samples previously included for QC }
  samptot = 0;                          { all samples }

postproc
  write( " " );
  write( tr("Summary of Samples selected for external QC"));
  write( "-----------------------------------------------" );
  write( " " );
  write( tr("Samples logged in (scanned)         %6d"), samptot );
  write( tr("Samples with final result           %6d"), sampfin );
  write( tr("Samples previously included for QC  %6d"), sampprv );
  write( tr("Samples with final result other     %6d"), sampoth );
  write( tr("Samples with protocol incomplete    %6d"), sampinc );
  write( " " );
  write( tr("Samples analyzed and selected for external retesting"));
  write( tr(".......Total positives %6d Selected %6d"), samppos, selpos );
  write( tr(".......Total negatives %6d Selected %6d"), sampneg, selneg );

PROC LEVEL1
  samptot = samptot + 1;               { total DBS samples }
  if samptot = 1 then
    write(tr("DBS Samples selected for external quality Control (QC)") );
    write(tr("Lab-ID  Bar Code"));
    write( "------------------------------------------------------" );
    write( " " );
  endif;

  if LSTATUS in 9 & LFINRES in 0,1 then   { protocol completed with a valid result }
    sampfin = sampfin + 1;
	if LFINRES = 1 then
	  samppos = samppos + 1;
    else
      sampneg = sampneg + 1;
    endif;	  
    if LEXT = notappl then               { sample never incuded in QC }
      LEXT = 0;
      sampeqc = sampeqc + 1;
      if sampeqc = nexteqc then
        LEXT    = 1;
        if LFINRES = 1 then		
          selpos  = selpos + 1;
		  testres = tr("Positive")
        elseif LFINRES = 0 then		
          selneg  = selneg + 1;
		  testres = tr("Negative")
        endif;		  
        nexteqc = nexteqc + inceqc;
        write( "%6d    %s    %s", LABID, LBAR, testres  );
      endif;
    else                              { sample previuosly included in QC }
      sampprv = sampprv + 1;
    endif;
  elseif LFINRES = 6 then              { count samples with result other }
    sampoth = sampoth + 1;
  else                                { count samples with protocol incomplete }
    sampinc = sampinc + 1;
  endif;
